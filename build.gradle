plugins {
    id 'java-library'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
}

repositories {
    mavenCentral()
}

bootJar.enabled = false

subprojects {
    group = 'com.weverse'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '17'

    repositories {
        mavenCentral()
    }

    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        //springdoc
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

        //lombok
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        //validation
        implementation 'org.springframework.boot:spring-boot-starter-validation'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    test{
        useJUnitPlatform()
    }
}

project(':module-application'){
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'

        implementation project(':module-domain')
        implementation project(':module-external')
        implementation project(':module-common')
    }
}

project(':module-common'){
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
    }
}

project(':module-domain'){
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        //cache
        api 'org.springframework.boot:spring-boot-starter-cache'
        api 'com.github.ben-manes.caffeine:caffeine'

        //database
        runtimeOnly 'com.h2database:h2'

        //mapstruct
        api 'org.mapstruct:mapstruct:1.5.5.Final'
        annotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"
        annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

        //jpa
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

        //querydsl
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
        annotationProcessor "jakarta.persistence:jakarta.persistence-api"
        annotationProcessor "jakarta.annotation:jakarta.annotation-api"

        implementation project(':module-common')
        implementation project(':module-external')
    }
}

project(':module-external'){
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation project(':module-common')
    }
}
